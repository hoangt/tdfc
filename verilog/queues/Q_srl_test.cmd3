#!/bin/csh
#
# Run Q_srl_*.v through synplify with different depth/width parameters
#
# Synplify 7.1
# Eylon Caspi,  3/22/04

set verilog_file_out   = Q_srl_test.v
set verilog_module_in  = Q_srl
set verilog_module_out = Q_srl_test

foreach verilog (Q_srl_prenone.v Q_srl_prefull.v Q_srl_prezero.v Q_srl_preboth.v Q_srl_prefull_SIMPLE.v Q_srl_prezero_SIMPLE.v Q_srl_preboth_SIMPLE.v)
  foreach depth (2 4 8 16 32 64 128 256)
    foreach width (1 2 4 8 16 32 64 128 256)
      if (($depth == 128) && ($width >= 256)) \
        continue					# - too slow
      if (($depth == 256) && ($width >= 128)) \
        continue					# - too slow	
      echo "Testing  { $verilog $depth $width }  --  forming $verilog_file_out"
      perl -npe "s/module $verilog_module_in/module $verilog_module_out/;   \
		 s/parameter depth = .*;/parameter depth = $depth;/;        \
		 s/parameter width = .*;/parameter width = $width;/; "      \
		< $verilog > $verilog_file_out
      foreach rev (rev_1__speed rev_2__area rev_3__no_optim)
	echo "Testing  { $verilog $depth $width }  --  compiling $rev"
	set synplify_dir = proj/$verilog_module_out
	set cmd = "synplify_pro -batch $synplify_dir/*.prj -impl $rev"
	echo $cmd
#	$cmd
	echo "Testing  { $verilog $depth $width }  --  results $rev"
	perl -e								    \
	  'while (<>) {							    \
	     if (/^(clock|system) +(\d+(.\d+)?) MHz +(\d+(.\d)?)/i)	    \
	       { if ($speed==0 || $speed>$4) { $speed=$4; } }		    \
	     elsif (/^Total +LUTs: +(\d+)/i)				    \
	       { if ($area==0  || $area<$1)  { $area=$1;  } }		    \
	     elsif (/^Register bits not including I\/Os: +(\d+)/i)	    \
	       { if ($regs==0  || $regs<$1)  { $regs=$1;  } }		    \
	     elsif (/^Output Ports:/)					    \
	       { if (\!$do_outs && \!$done_outs) { $do_outs=1;    } }	    \
	     elsif (/^i_b[^\d]+(\d+(\.\d+)?)/)				    \
	       { if (  $do_outs && \!$done_outs) { $delay_i_b=$1; } }	    \
	     elsif (/^o_v[^\d]+(\d+(\.\d+)?)/)				    \
	       { if (  $do_outs && \!$done_outs) { $delay_o_v=$1; } }	    \
	     elsif (/^========/)					    \
	       { if (  $do_outs && \!$done_outs) { $done_outs=1;  } }	    \
	   }								    \
	   print "$speed MHz	$area LUTs	$regs FFs	" .	    \
		 "$delay_i_b ns i_b	$delay_o_v ns o_v\n" '		    \
	   $synplify_dir/$rev/${verilog_module_out}.srr
      end
    end
  end
end
