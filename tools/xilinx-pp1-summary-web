#!/bin/csh
#
#  Generate Xilinx report files for all apps in this directory
#  Call after synthesis (synplify-all / xilinx-all)
#
#  Xilinx ISE 5.2i
#  Eylon Caspi,  10/19/03

# set toolpath = ${0:h}
set toolpath = ${cwd}/../../tools

set HTMLGEN  = ${toolpath}/xilinx-pp1-summary-html
set XLSGEN   = ${toolpath}/xilinx-pp1-summary-xls

set HTMLDIR = ~/public_html/brass/everilog/tdfc-1.155+pp1F/xilinx
set XLSDIR  = ~/public_html/brass/everilog/tdfc-1.155+pp1F/xilinx/EXCEL/ORIG

mkdir -p $HTMLDIR
mkdir -p $XLSDIR

# apps = (names of all directories in current dir, w/o leading path)
set apps = (`find . -mindepth 1 -maxdepth 1 -type d -print | \
             perl -npe 's{./}{}'` )

foreach app ($apps)
  echo "Generating Xilinx web report for" $app
  set projdir = ${app}/proj-xilinx
  if (-d $projdir) then
    cd $projdir
    ${HTMLGEN} > xilinx-pp1-summary.html
    cp -fp xilinx-pp1-summary.html ${HTMLDIR}/${app}.html
    ${XLSGEN}  > xilinx-pp1-summary.xls
    cp -fp xilinx-pp1-summary.xls  ${XLSDIR}/${app}.xls
    cd ../..
  else
    echo "  Error: no directory $projdir"
  endif
end
