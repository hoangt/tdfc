#!/bin/tcsh
#
# Generate tab-delimited (Excel compatible) summary report
#   for every "-pp1" page project in this directory,
#   for every implementation thereof,
#   compiling both its page composition and its behavioral operator
# Assume pages were generated by "tdfc -synplify -pp1",
#   so every base module <op> induces a page _page_<op>
# Use after synthesis (synplify-all, synplify-pages-all)
#
# Synplify 7.7
# Eylon Caspi, 10/16/03 9/29/04

set toolpath = ${0:h}

# set topmodules = `find . -maxdepth 1 -mindepth 1 -type d -name _page_\* -print | perl -npe 's{.*/}{}; s{_qin$}{}; s{_q$}{}; s{_noin$}{}' | sort | uniq`
set topmodules = `find . -maxdepth 1 -mindepth 1 -type d -name \*_noin -print | perl -npe 's{.*/}{}; s{_noin$}{}'`

echo "Synplify results for:  ${cwd:h}"
echo `date`
echo ""
echo "Name\tComponent\tModule\tRevision\tClock (MHz)\tLUTs\tFFs\tLUT-FFs\tFSM Style\t"

foreach topmodule ($topmodules)
  set basemodule = ${topmodule:s/_page_//}
  set components = (fsm dp dpfsm qin q dpq noin top)
  set modules    = (${basemodule}_fsm ${basemodule}_dp ${basemodule}	\
		    ${topmodule}_qin ${topmodule}_q			\
		    ${topmodule}_dpq ${topmodule}_noin ${topmodule})
  foreach i (1 2 3 4 5 6 7 8)
    set component = ${components[$i]}
    set module    = ${modules[$i]}
    if (-d $module) then
      foreach revpath ($module/rev*)
        set rev = ${revpath:t}
        set srrfile = $revpath/${module}.srr
        echo -n "${basemodule}\t${component}\t${module}\t${rev}\t"
        if (-f $srrfile) then
	  set results = "`${toolpath}/synperf-concise ${srrfile}`"
  	  echo "$results\t"
        else
          echo "\t\t\t\t\t"
        endif
      end
    endif
  end
end
