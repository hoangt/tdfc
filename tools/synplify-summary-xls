#!/bin/tcsh
#
# Generate tab-delimited (Excel compatible) summary report
#   for every behavioral project in this directory,
#   every implementation thereof
# Use after synthesis (synplify-all)
# Report format:
#
#                  rev_1__speed            rev_2__area    rev_3__no_optim
#                  speed area regs style   sp ar re st    sp ar re st
# module_dp        .     .    .    .       .  .  .  .     .  .  .  .
# module_fsm       .     .    .    .       .  .  .  .     .  .  .  .
# module           .     .    .    .       .  .  .  .     .  .  .  .
# ...
#
# Synplify 7.1
# Eylon Caspi, 6/8/03

set topmodules = `find . -maxdepth 1 -mindepth 1 -type d -print | perl -npe 's{.*/}{}; s{_fsm$}{}; s{_dp$}{};' | sort | uniq | egrep -v '^_page_'`

echo "Synthesis results for:  ${cwd:h}"
echo `date`
echo ""
echo "\trev_1__speed\t\t\t\trev_2__area\t\t\t\trev_3__no_optim\t\t\t\t"
echo -n "\t"
repeat 3 \
  echo -n "Speed\tArea\tRegs\tFSM\t"
echo ""
echo -n "\t"
repeat 3 \
  echo -n "(MHz)\t(LUTs)\t\tStyle\t"
echo ""

foreach topmodule ($topmodules)
  set modules = (${topmodule}_dp ${topmodule}_fsm ${topmodule})
  foreach module ($modules)
    echo -n "$module\t"
    if (-d $module) then
      foreach revpath ($module/rev*)
        set rev = ${revpath:t}
        set srrfile = $revpath/${module}.srr
        if (-f $srrfile) then
          perl -e 'foreach (<>) {                                             \
		     if (/^(clock|system) +(\d+(.\d+)) MHz +(\d+(.\d+)) MHz/i)\
		       { if ($speed==0 || $speed>$4) { $speed=$4; } }         \
		     if (/^Adding property syn_encoding, value "([^"]+)"/i)   \
		       { $fsmstyle = $1; }                                    \
		     if (/^Total +LUTs: (\d+)/i)                              \
		       { if ($area==0 || $area<$1) { $area = $1 } }           \
		     if (/^Register bits not including I\/Os: +(\d+)/i)       \
		       { if ($regs==0 || $regs<$1) { $regs = $1 } }           \
               }                                                              \
	           print "$speed\t$area\t$regs\t$fsmstyle\t"' $srrfile
        else
          perl -e 'print "\t\t\t\t"'
        endif
      end
    endif
    echo ""
  end
end
